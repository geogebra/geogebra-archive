






SetGlobalLazyVariable(I,Complex(0,1));






LocalSymbols(CacheOfConstantsN) [



RuleBase("CachedConstant", {C'cache, C'atom, C'func});
UnFence("CachedConstant", 3);	
HoldArg("CachedConstant", C'func);
HoldArg("CachedConstant", C'cache);	

Rule("CachedConstant", 3, 10, And(IsAtom(C'atom), IsFunction(C'func)))
[
 	Local(C'name,C'functionName);
	Set(C'name, String(C'atom));	
  Set(C'functionName,ConcatStrings("Internal'",C'name));

	If(	
		IsAtom(Eval(C'cache)),
		MacroSet(Eval(C'cache), {})
	);

	
	If(
	  Equals(Builtin'Assoc(C'name, Eval(C'cache)), Empty),	
	  [	
		MacroClear(C'atom);

		

		DestructiveInsert(Eval(C'cache), 1, {C'name, 0, 0});

		
		

		MacroRuleBase(C'functionName, {});
		`( Rule(@C'functionName, 0, 1024, True)
		[
			Local(new'prec, new'C, cached'C);
			Set(new'prec, BuiltinPrecisionGet());
			// fetch the cache entry for this constant
			// note that this procedure will store the name of the cache here in this statement as Eval(C'cache)
			Set(cached'C, Builtin'Assoc(@C'name, @C'cache));
			If( MathNth(cached'C, 2) != new'prec,
			  [	// need to recalculate at current precision
				If(Equals(InVerboseMode(),True), Echo("CachedConstant: Info: constant ", @C'name, " is being recalculated at precision ", new'prec));
				Set(new'C, RoundTo(Eval(@C'func),new'prec));
				DestructiveReplace(cached'C, 2, new'prec);
				DestructiveReplace(cached'C, 3, new'C);
				new'C;
			  ],
			  // return cached value of C'atom
			  MathNth(cached'C, 3)
			);
		]);

		

		
	  ],
	  
	  Echo("CachedConstant: Warning: constant ", C'atom, " already defined")
	);
];

Rule("CachedConstant", 3, 20, True)
	Echo("CachedConstant: Error: ", C'atom, " must be an atom and ", C'func, " must be a function.");



Function("AssignCachedConstantsN", {})
[
	Local(var,fname);
	ForEach(var, AssocIndices(CacheOfConstantsN))
	[
		MacroClear(Atom(var));
    Set(fname,ConcatStrings("Internal'",var));
    Set(var,Atom(var));
		
    `SetGlobalLazyVariable((@var), UnList({Atom(fname)}));
	];
];
UnFence("AssignCachedConstantsN", 0);



Function("ClearCachedConstantsN", {})
[
	Local(c'entry);
	ForEach(c'entry, CacheOfConstantsN)
		MacroClear(Atom(c'entry[1]));
];
UnFence("ClearCachedConstantsN", 0);


CachedConstant(CacheOfConstantsN, Pi,
[


	Local(result,old'prec);
  Set(old'prec,BuiltinPrecisionGet());
If(Equals(InVerboseMode(),True), Echo("Recalculating Pi at precision ",old'prec+5));
	BuiltinPrecisionSet(BuiltinPrecisionGet()+5);
	result := MathPi();
If(Equals(InVerboseMode(),True),Echo("Switching back to precision ",old'prec));
	BuiltinPrecisionSet(old'prec);
	result;
]
);
CachedConstant(CacheOfConstantsN, gamma, GammaConstNum());
CachedConstant(CacheOfConstantsN, GoldenRatio, N( (1+Sqrt(5))/2 ) );
CachedConstant(CacheOfConstantsN, Catalan, CatalanConstNum() );

]; 

