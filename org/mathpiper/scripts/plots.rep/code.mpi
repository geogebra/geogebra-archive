





ListToHash(list) :=
[
	Local(item, result);
	result := {};
	ForEach(item, list)
		If(
			IsFunction(item) And (Type(item) = "=" Or Type(item) = "==") And IsAtom(item[1]),
			result[String(item[1])] := If(
				IsAtom(item[2]) And Not IsNumber(item[2]) And Not IsString(item[2]),
				String(item[2]),
				item[2]
			),
			Echo({"ListToHash: Error: item ", item, " is not of the format a=b or a==b"})
		);
	result;
];

HoldArgNr("ListToHash", 1, 1);



sign'change(x,y,z) :=
If(
	IsNumber(x) And IsNumber(y) And IsNumber(z)
	And Not (
		x>y And y<z
			Or
		x<y And y>z
	)
, 0, 1); 





WriteDataItem(tuple_IsList, _options'hash) <--
[
  Local(item);
  If(	
  	IsNumericList(tuple),
	ForEach(item,tuple)
	[
		Write(item);
		Space();
	]
  );
  NewLine();
];


10 # RemoveRepeated({}) <-- {};
10 # RemoveRepeated({_x}) <-- {x};
20 # RemoveRepeated(list_IsList) <-- [
	Local(i, done);
	done := False;
	For(i:=0, Not done, i++)
	[
		While(i<Length(list) And list[i]=list[i+1])
			DestructiveDelete(list, i);
		If(i=Length(list), done := True);
	];
	list;
];


